; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "moslem tools, alcoder"
#define MyAppVersion "3.8"
#define MyAppPublisher "abd alrhman mohamed alcoder"
#define MyAppURL "https://github.com/MesterAbdAlrhmanMohmed"
#define MyAppExeName "moslem_tools.exe"
#define MyAppAssocName MyAppName + " File"
#define MyAppAssocExt ".myp"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

[Setup]
AppId={{E41F7AC1-AF39-4E08-B56B-967313455FAC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
OutputBaseFilename=mysetup
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; نسخ exe الرئيسي
Source: "D:\alcoder\moslem tools\moslemTools\dist\moslem_tools\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
; نسخ باقي الملفات (مكتبات، إلخ)
Source: "D:\alcoder\moslem tools\moslemTools\dist\moslem_tools\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; نسخ مجلد data إلى C:\Users\<User>\data
Source: "D:\alcoder\moslem tools\moslemTools\dist\moslem_tools\data\*"; DestDir: "{code:GetUserProfileDataDir}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Registry]
; ربط الامتداد
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKA; Subkey: "Software\Classes\Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".myp"; ValueData: ""

; تشغيل من Run (Win+R) بكتابة mt
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\mt.exe"; ValueType: string; ValueName: ""; ValueData: "{app}\{#MyAppExeName}"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\App Paths\mt.exe"; ValueType: string; ValueName: "Path"; ValueData: "{app}"

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; HotKey: "CTRL+ALT+M"

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
  DeleteAllData: Boolean;

function GetUserProfileDataDir(Param: String): String;
var
  UserProfile: String;
begin
  UserProfile := GetEnv('USERPROFILE');
  Result := UserProfile + '\data';
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  UserDataDir, LocalAppDir, RoamingAppDir: String;
begin
  if CurUninstallStep = usUninstall then
  begin
    if MsgBox('Do you want to completely remove all files including user data and settings?', mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
    begin
      DeleteAllData := True;
      Log('User chose to delete all data.');
    end
    else
    begin
      DeleteAllData := False;
      Log('User chose to keep data.');
    end;
  end;

  if (CurUninstallStep = usPostUninstall) and DeleteAllData then
  begin
    UserDataDir := GetUserProfileDataDir('');
    LocalAppDir := ExpandConstant('{localappdata}\{#MyAppName}');
    RoamingAppDir := ExpandConstant('{userappdata}\{#MyAppName}');

    DelTree(UserDataDir, True, True, True);
    DelTree(LocalAppDir, True, True, True);
    DelTree(RoamingAppDir, True, True, True);

    Log('All user data and AppData directories have been deleted.');
  end;
end;
